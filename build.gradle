buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
    }
}

apply plugin: 'net.minecraftforge.gradle'

apply plugin: 'maven-publish'

version = "1.1.0"
group = "it.hurts.metallurgy_reforged"
archivesBaseName = "Metallurgy-4-Reforged"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }

        server {

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }
    }
}

repositories {

    jcenter()

    //location of the maven that hosts JEI & TCon
    maven {
        name = "Progwml6 maven"
        url = "http://dvs1.progwml6.com/files/maven"
    }

    //JEI maven fallback
    maven {
        name = "ModMaven"
        url = "modmaven.k-4u.nl"
    }

    //Jared's Maven Repository (CraftTweaker)
    maven {
        name 'jared maven'
        url "http://maven.blamejared.com/"
    }

    //CurseForge maven as a final fallback
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
}

dependencies {

    implementation('org.codehaus.groovy:groovy-all:2.5.7')

    minecraft fg.deobf('net.minecraftforge:forge:1.12.2-14.23.5.2854')

    //Compile against the JEI API but do not include it at runtime
    compileOnly fg.deobf('mezz.jei:jei_1.12.2:4.16.+:api')

    //At runtime, use the full JEI jar
    //runtimeOnly fg.deobf('mezz.jei:jei_1.12.2:4.16.+')

    //Tinker Construct & Mantle
    compileOnly fg.deobf('slimeknights.mantle:Mantle:1.12-1.3.3.55')
    compileOnly fg.deobf('slimeknights:TConstruct:1.12.2-2.13.0.189')

    compileOnly 'constructs-armory:conarm:1.12.2:1.2.5.9'

    //Industrial Foregoing
    compileOnly fg.deobf('industrial-foregoing:industrialforegoing-1.12.2:1.12.13:237')

    //CraftTweaker
    compile fg.deobf('CraftTweaker2:CraftTweaker2-API:4.1.20.586')
    compile fg.deobf('CraftTweaker2:ZenScript:4.1.9.6')


    //ProjectE
    compileOnly 'projecte:ProjectE:1.12.2:PE1.4.1'

    //Silent's Gems
    compile 'silent-lib:SilentLib:1.12.2:3.0.14+168'
    compile 'silents-gems:SilentsGems:1.12.2:2.8.22+327'

    //Immersive Engineering
    compileOnly fg.deobf('immersive-engineering:ImmersiveEngineering:0.12:98')
}


jar {
    manifest {
        attributes([
                "Specification-Title": "metallurgy",
                "Specification-Vendor": "davoleo",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"davoleo",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    destinationDir = file 'output'
}

clean {
    delete 'output'
    delete 'mcmodsrepo'
}

//Maven Repository
publishing {
    publications {
        metallurgy(MavenPublication) {
            groupId = 'io.github.davoleo'
            artifactId = 'metallurgy-4-reforged'
            from components.java
        }
    }
    repositories {
        maven {
            name "local"
            url "file:///${project.projectDir}/mcmodsrepo"
        }

        maven {
            name "github"
            url "https://maven.pkg.github.com/Davoleo/Metallurgy-4-Reforged"
            credentials {
                username = ""
                password = ""
            }
        }
    }
}