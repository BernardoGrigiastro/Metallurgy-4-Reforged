buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

apply plugin: 'maven-publish'

/*
 * -------------------------------------------------------------------------------------------------------
 * Class: build
 * This class is part of Metallurgy 4 Reforged
 * Complete source code is available at: https://github.com/Davoleo/Metallurgy-4-Reforged
 * This code is licensed under GNU GPLv3
 * Authors: Davoleo, ItHurtsLikeHell, PierKnight100
 * Copyright (c) 2020.
 * --------------------------------------------------------------------------------------------------------
 */

// Non Modificare Sopra

version = "1.0.2"
group = "it.hurts.metallurgy_reforged"
archivesBaseName = "Metallurgy-4-Reforged"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
	mappings = "snapshot_20180814"
}

repositories {

    jcenter()

    //location of the maven that hosts JEI & TCon
	maven {
		name = "Progwml6 maven"
		url = "http://dvs1.progwml6.com/files/maven"
	}
  
    //JEI maven fallback
	maven {
		name = "ModMaven"
		url = "modmaven.k-4u.nl"
	}
    
    //Jared's Maven Repository (CraftTweaker)
    maven {
        name 'jared maven'
        url "http://maven.blamejared.com/"
    }
	
    //CurseForge maven as a final fallback
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
}

dependencies {

    implementation('org.codehaus.groovy:groovy-all:2.5.7')
    
    //deobfProvided: Direct Source Code
    //runtime: only at runtime
    //deobfCompile: Compiled De-obfuscated Source Code
    //compile: Compiled Source Code
    //compileOnly: NO runtime

    //Compile against the JEI API but do not include it at runtime
    deobfProvided "mezz.jei:jei_1.12.2:4.15.+:api"
	
    //At runtime, use the full JEI jar
    runtime "mezz.jei:jei_1.12.2:4.15.+"
	
    //Tinker Construct & Mantle
	deobfCompile "slimeknights.mantle:Mantle:1.12-1.3.3.56"
    deobfCompile "slimeknights:TConstruct:1.12.2-2.13.0.184"
    
    deobfCompile "constructs-armory:conarm:1.12.2:1.2.5.9"
     
    //Industrial Foregoing
    compileOnly "industrial-foregoing:industrialforegoing-1.12.2:1.12.13:237"

    //CraftTweaker
    deobfProvided "CraftTweaker2:CraftTweaker2-API:4.1.20.577"

    //ProjectE
	compileOnly "projecte:ProjectE:1.12.2:PE1.4.1"

    //Silent's Gems
    compile "silent-lib:SilentLib:1.12.2:3.0.14+168"
    deobfCompile "silents-gems:SilentsGems:1.12.2:2.8.22+327"
}

processResources {
    
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    destinationDir = file 'output'
}

sourceJar {
    destinationDir = file 'output'
}

clean {
    delete 'output'
    delete 'mcmodsrepo'
}

//Maven Repository
publishing {
    publications {
        metallurgy(MavenPublication) {
            groupId = 'io.github.davoleo'
            artifactId = 'metallurgy-4-reforged'
            from components.java
        }
    }
    repositories {
        maven {
            name "local"
            url "file:///${project.projectDir}/mcmodsrepo"
        }

        maven {
            name "github"
            url "https://maven.pkg.github.com/Davoleo/Metallurgy-4-Reforged"
            credentials {
                username = ""
                password = ""
            }
        }
    }
}